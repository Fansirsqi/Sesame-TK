import java.security.MessageDigest
import java.text.SimpleDateFormat

plugins {
    id "com.android.application"
    id 'org.jetbrains.kotlin.android'
}

android {
    // 禁用所有单元测试
    testOptions {
        unitTests.all {
            it.enabled = false
        }
    }

    // 启用 BuildConfig 生成
    buildFeatures {
        buildConfig = true
    }

    // 版本号配置
    def major = 0          // 主版本号
    def minor = 2          // 次版本号
    def patch = 6          // 补丁版本号
    def buildTag = "alpha" // 构建标签 (alpha, beta, release, 等)
    def buildDate
    def buildTime
    def buildTargetCode
    def _versionCode
    def _versionName
    def gitCommitCount = 'git rev-list --count HEAD'.execute().text.trim()

    // 生成构建时间和目标代码
    try {
        def dateFormat = new SimpleDateFormat("yy-MM-dd")
        buildDate = dateFormat.format(new Date())
        def timeFormat = new SimpleDateFormat("HH:mm:ss")
        buildTime = timeFormat.format(new Date())
        MessageDigest md = MessageDigest.getInstance("MD5")
        md.update(buildTime.bytes)
        buildTargetCode = md.digest().encodeHex().toString().substring(0, 4)
    } catch (Exception e) {
        println("时间或日期格式化错误: ${e.message}")
        buildDate = "unknown"
        buildTime = "unknown"
        buildTargetCode = "0000"
    }

    // 设置版本号和版本名称
    _versionCode = gitCommitCount.empty ? 9527 : gitCommitCount.toInteger()
    if (buildTag.contains("alpha") || buildTag.contains("beta")) {
        _versionName = "${major}.${minor}.${patch}-${buildTag}.${buildTargetCode}"
    } else {
        _versionName = "${major}.${minor}.${patch}-${buildTag}"
        buildTag = ""
    }

    // 配置 Android 构建设置
    namespace "fansirsqi.xposed.sesame"
    compileSdk 34


    defaultConfig {
        minSdk 21
        //noinspection OldTargetApi
        targetSdk 34
        versionCode _versionCode
        versionName _versionName
        buildConfigField "String", "BUILD_DATE", "\"${buildDate}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "BUILD_NUMBER", "\"${buildTargetCode}\""
        buildConfigField "String", "BUILD_TAG", "\"${buildTag}\""
        if (!System.getenv('CI')) {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        } else {
            println("CI环境下不编译jniLibs")
            sourceSets {
                main {
                    jniLibs.srcDirs = ['src/main/jniLibs']
                }
            }
        }


    }
    if (!System.getenv('CI')) {
        println("非CI环境:编译jniLibs")
        externalNativeBuild {
            cmake {
                path file("src/main/cpp/CMakeLists.txt")
                version "3.31.6"
                ndkVersion '29.0.13113456'
            }
        }
    }

    ext {
        applicationName = "Sesame"
        applicationType = "Normal"
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    // 产品风味配置
    flavorDimensions.add("default")
    productFlavors {
        normal {
            dimension "default"
            ext.applicationType = "Normal"
        }
        compatible {
            dimension "default"
            ext.applicationType = "Compatible"
        }
    }
    signingConfigs {
        debug {
        }
    }

    // 构建类型配置
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix "-debug"
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    // 编译选项
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17

    }


    // 配置 APK 输出文件名
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                } else {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}-debug.apk"
                }
            }
        }
    }
}

// 依赖项配置
dependencies {
    //noinspection GradleDependency
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.github.tony19:logback-android:3.0.0'
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.1'
    //noinspection GradleDependency
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'
    compileOnly 'de.robv.android.xposed:api:82'
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    def jacksonVersion = '2.18.1'
    normalImplementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    normalImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    normalImplementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    def jacksonCompatVersion = '2.13.5'
    compatibleImplementation "com.fasterxml.jackson.core:jackson-core:${jacksonCompatVersion}"
    compatibleImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonCompatVersion}"
    compatibleImplementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonCompatVersion}"
}